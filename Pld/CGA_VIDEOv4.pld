Name            CGAVIDEOV4 100 Pin TQFP Thin Quad Flat Package in 0.1" adapter using ATF1508AS;
Partno          CGAVIDEOV4;
Revision        4;
Date            25/5/25;
Designer        Dave Henry;
Company         ;
Location        UK;
Assembly        None;
Device          f1508isptqfp100;

/* 04/05/2025 V1 Initial version to replace most video side logic clock & blinksync   */
/*               Note the fitter does mot lik legal variable 640BW changed to BW640!  */
/* 05/05/2025 V2 Add control and colour registers                                     */
/* 06/05/2025 V3 Add status register and make pinnodes flexible - don't assign MC     */
/* 25/05/2025 V4 Switch to ATF1508 BBPC Video 12 & 13                                 */
/*               Note HIRES & VIDEOEN hard coded to 1 to make DotClock 25Mhz for 80   */
/*               column mode and enable CLR_SR                                        */
/**************************************************************************************/
/*                                                                                    */
/**************************************************************************************/

/* Select ATF1508 Fitter Property list */
/* 1) Preassign Keep is on - Keep User assigned pins */
/* 2) Disable Pin Keeper Circuits */
/* 3) Enable fast slew rate */

PROPERTY ATMEL {preassign keep};
PROPERTY ATMEL {pin_keep OFF} ; /* Disables pin-keeper circuits */
PROPERTY ATMEL {OUTPUT_FAST ON};
PROPERTY ATMEL { jtag=on };

/* Pin Map for 100 pin Breadboard TQFP adapter.
Note pin layout different from .FIT file pin 26 moved from bottom to left, 76 from top to right. Rows in top and bottom swapped.
Block A wont fit. Try moving P0 from MC13 Pin 96 to MC54 pin 69 and VSYNC from MC16 Pin 92 to MC7 pin 6
                                  D       R   C     C C   D C C                              
                                H O V     E   L     H U   E H H                            
                    P P P P P   S T S     S   K     A R     A /                           
                    4 3 2 1 0   Y C Y   G E   2     T S     R A                          
                                N L N   C T   5     D O     C T                          
                                C K C   L           L R     L 
                                      V K           Y       K                          
                                      C 2     G   M M M   M M M M M
                            M   M M M C _ G   C   C C C   C C C C C
                    M M M M C G C C C I O C O L G 1 1 1 V 1 1 1 1 1
                    C C C C 1 N 1 1 1 N E L E K N 2 2 2 C 2 2 2 1 1
                    5 6 8 9 1 D 3 4 6 T 2 R 1 1 D 8 6 5 C 3 1 0 8 7
Even inside       ----------------------------------------------------                     
Odd outside      /    99  97  95  93  91  89  87  85  83  81  79  77  \  Odd Outside pins                 
                /  100  98  96  94  92  90  88  86  84  82  80  78     \ Even Inside pins                 
           MC3 | 1                                                   76 | MC115 HS  Even Inside pins breadboard        
           MC1 | 2                                                   75 | MC113 VS  Odd Outside pins header        
           VCC | 3                                                   74 | GND              
           TDI | 4                                                   73 | TDO              
          MC30 | 5                                                   72 | MC110               
          MC29 | 6                                                   71 | MC109            
          MC27 | 7                                                   70 | MC107 D7              
          MC25 | 8                                                   69 | MC105
          MC24 | 9                                                   68 | MC104 D6            
          MC22 | 10                                                  67 | MC102   
           GND | 11                                                  66 | VCC              
          MC21 | 12                     ATF1508                      65 | MC101           
          MC19 | 13                  100-Lead TQFP                   64 | MC99 D5
          MC17 | 14                                                  63 | MC97 /CGACOL            
           TMS | 15                                                  62 | TCK              
  BLINKEN MC46 | 16                                                  61 | MC94 /CGACTL            
  VIDEOEN MC45 | 17                                                  60 | MC93 D4         
           VCC | 18                                                  59 | GND              
       BW MC43 | 19                                                  58 | MC91 D3            
       Qa MC41 | 20                                                  57 | MC89 /CGASTAT          
       Qb MC40 | 21                                                  56 | MC88 D2            
          MC38 | 22                                                  55 | MC86               
       Qc MC37 | 23                                                  54 | MC85 D1            
          MC35 | 24                                                  53 | MC83               
       Qd MC33 | 25                                                  52 | MC81 DO      
           GND | 26                                                  51 | VCC 
                \     28  30  32  34  36  38  40  42  44  46  48  50   /  Odd Outside pins                 
                 \  27  29  31  33  35  37  39  41  43  45  47  49    /   Even Inside pins                 
                  ----------------------------------------------------                     
                    M M M M M M M V M M M G V M M M G M M M M M M M
                    C C C C C C C C C C C N C C C C N C C C C C C C
                    6 6 6 5 5 5 5 C 5 5 4 D C 6 6 6 D 7 7 7 7 7 7 8
                    4 2 1 9 7 6 4   3 1 9   I 5 7 9   0 2 3 5 7 8 0
                        V V V V V   V V V   N C D S   A C G H B
                        D D D D D   D D D   T H O D   T L R I W
                        7 6 5 4 3   2 1 0     L T O   S R P R 6
                                              A C T   R _ H E 4
                                              T L S   L S   S 0
                                              C K     A R
                                              H 2     T  
                                                      C
                                                      H


VCC = Supply Voltage for IO pin which must be connected to (5.0V or 3.3V)
VCCINT = VCC +5V Internal
GND = GND pin which must be connected to ground
TMS,TDI,TDO,TDI = JTAG pins which must reserved for the JTAG interface
NC = Unused I/O pins which must be unconnected on the board
*/

/*
 * JTAG: For programming, don't use. Re-enable JTAG support 12V on OE1 via 1K8 Resistor
 *  
 *  Pin 4  TDI MC8
 *  Pin 15 TMS MC32
 *  Pin 62 TCK MC48
 *  Pin 73 TDO MC56
 *  
 */

/*
 * Inputs:  
 */
Pin 90 = GCLK2_OE2;
Pin 89 = !RESET; /* GCLR; */
Pin 88 = OE1; /* Also 12V via 1K8 Resistor will restore JTAG programming if disabled */
Pin 87 = CLK25; /* GCLK1 Global clock set to 25.175MHz VGA dot clock */
/* Note GLK3 also Pin 85 MC128 */


/*
 * Inputs/Outputs:  
 */
/* Group A MC1-MC16 */
Pin 2       = MC1; 
Pin 1       = MC3;   /* MC3; */ 
Pin 100     = P4;   /* Output to Video DAC bit 4 MC5; */
Pin 99      = P3;   /* Output to Video DAC bit 3 MC6; */
Pin 98      = P2;   /* Output to Video DAC bit 2 MC8; */
Pin 97      = P1;   /* Output to Video DAC bit 1 MC9; */
Pin 96      = P0;   /* Output to Video DAC bit 0 MC11; */
Pin 94      = HSYNC; /* Horizontal Sync output to VGA, inverted and delayed MC13; */
Pin 93      = DOTCLK; /* MC14; */
Pin 92      = VSYNC; /* Vertical Sync output to VGA, not inverted and delayed MC16; */

/* Group B MC17-MC32 */
Pin 14      = MC17;
Pin 13      = MC19;
Pin 12      = MC21;
Pin 10      = MC22;
Pin 9       = MC24;
Pin 8       = MC25;
Pin 7       = MC27;
Pin 6       = MC29;
Pin 5       = MC30;
/* Pin 4 MC32 TDI JTAG Do Not use */

/* Group C MC33-MC48 */
Pin 25      = Qd;      /* MC33; */
Pin 24      = MC35;
Pin 23      = Qc;      /* MC37; */
Pin 22      = MC38;
Pin 21      = Qb;      /* MC40; */
Pin 20      = Qa;      /* MC41; */
Pin 19      = BW;      /* Control Port D2 MC43; */
Pin 17      = VIDEOEN; /* Control Port D3 MC45; */
Pin 16      = BLINKEN; /* Control Port D5 MC46; */
/* Pin 15 MC48 TMS JTAG Do Not use */

/* Group D MC49-MC64 */
Pin 37      = VD0in; /* MC49; */
Pin 36      = VD1in; /* MC51; */
Pin 35      = VD2in; /* MC53; */
Pin 33      = VD3in; /* MC54; */
Pin 32      = VD4in; /* MC56; */
Pin 31      = VD5in; /* MC57; */
Pin 30      = VD6in; /* MC59; */
Pin 29      = VD7in; /* MC61; */
Pin 28      = MC62;
Pin 27      = MC64;

/* Group E MC65-MC80 */
Pin 40      = !CHLATCH;   /* Needs reverse polarity MC65; */
Pin 41      = DOTCLK2;    /* MC67; */
Pin 42      = SDOTS;      /* MC69; */
Pin 44      = !ATSRLATCH; /* Needs reverse polarity MC70; */
Pin 45      = !CLR_SR;   /* MC72; */
Pin 46      = GRPH;      /* Control Port D1 MC73; */
Pin 47      = HIRES;     /* Control Port D0 MC75; */
Pin 48      = BW640;     /* Control Port D4 MC77; */
Pin 49      = MC78;
Pin 50      = MC80;


/* Group F MC81-MC96 */
Pin 52      = D0;        /* MC81; */
Pin 53      = MC83;
Pin 54      = D1;        /* MC85; */
Pin 55      = MC86;
Pin 56      = D2;        /* MC88; */
Pin 57      = CGASTAT;   /* MC89; */
Pin 58      = D3;        /* MC91; */
Pin 60      = D4;        /* MC93; */
Pin 61      = CGACTL;    /* CGA Control register select active low but will not use ! as need rising clock to .d MC94; */
/* Pin 62 MC96 TCK JTAG Do Not use */

/* Group G MC97-MC112 */
Pin 63      = CGACOL;    /* CGA Colour register select active low but will not use ! as need rising clock to .d MC97; */
Pin 64      = D5;        /* MC99; */
Pin 65      = MC101;     /* MC101; */
Pin 67      = MC102;     /* MC102; */
Pin 68      = D6;        /* MC104; */
Pin 69      = MC105;
Pin 70      = D7;        /* MC107; */
Pin 71      = MC109;     /* MC109; */
Pin 72      = MC110;
/* Pin 73 MC112 TDO JTAG Do Not use */

/* Group H MC113-MC128 */
Pin 75      = VS;        /* MC113; */
Pin 76      = HS;        /* MC115; */
Pin 77      = MC117;
Pin 78      = MC118;
Pin 79      = CHAT;      /* MC120; */
Pin 80      = !INVCHCLK; /* MC121; */
Pin 81      = DE;        /* MC123; */
Pin 83      = CURSOR;    /* MC125; */
Pin 84      = CHATDLY;   /* MC126; */
Pin 85      = MC128;


Pinnode = VD0;   /* Was MC17; */
Pinnode = VD1;   /* Was MC18; */ 
Pinnode = VD2;   /* Was MC19; */
Pinnode = VD3;   /* Was MC20; */
Pinnode = VD4;   /* Was MC21; */
Pinnode = VD5;   /* Was MC22; */
Pinnode = VD6;   /* Was MC23; */
Pinnode = VD7;   /* Was MC24; */

Pinnode = VSQ4; /* Counter Q4 for Blink Char & Cursor VS/32 */ 
Pinnode = VSQ3; /* Counter Q3 for Blink Char & Cursor VS/16 */ 
Pinnode = VSQ2; /* Counter Q2 for Blink Char & Cursor VS/8 */ 
Pinnode = VSQ1; /* Counter Q1 for Blink Char & Cursor VS/4 */ 
Pinnode = VSQ0; /* Counter Q0 for Blink Char & Cursor VS/2 */ 

Pinnode = CRBLUE;    /* MC41; */
Pinnode = CRGREEN;   /* MC43; */
Pinnode = CRRED;     /* MC45; */
Pinnode = CRINTEN;   /* MC47; */
Pinnode = CRBACKI;   /* MC49; */
Pinnode = CRCOLSL;   /* MC51; */

Pinnode = BLANKDE1;  /* MC61; */

25MHZ=CLK25; /* synonym for 25MHZ as Fitter doesn't like numeric starting variables */

FIELD   CPUDATA = [D5..0] ; /* Only D0-D5 used with Colour and Control registers */
/* FIELD   VIDDATA = [VD7..0] ;
FIELD   VIDDATAin = [VDin7..0] ; */

/* CPUDATA.OE = 'b'0;  D0-D7 will be input only - Fitter doesn't like this unlike ATF16V8 and ATF22V10 !*/

/* Define Colour register based on pinnodes for D0-D5 */
CRBLUE.d  = D0.io;
CRGREEN.d = D1.io;
CRRED.d   = D2.io;
CRINTEN.d = D3.io;
CRBACKI.d = D4.io;
CRCOLSL.d = D5.io;

CRBLUE.CK  = CGACOL;
CRGREEN.CK = CGACOL;
CRRED.CK   = CGACOL;
CRINTEN.CK = CGACOL;
CRBACKI.CK = CGACOL;
CRCOLSL.CK = CGACOL;

/* Latch Video Data in VDin0-7 to VD0-7 pinnodes using ATSTLATCH clock */
VD0.d = VD0in.io;
VD1.d = VD1in.io;
VD2.d = VD2in.io;
VD3.d = VD3in.io;
VD4.d = VD4in.io;
VD5.d = VD5in.io;
VD6.d = VD6in.io;
VD7.d = VD7in.io;

VD0.CK = !ATSRLATCH; /* Does this need ! ? */
VD1.CK = !ATSRLATCH;
VD2.CK = !ATSRLATCH;
VD3.CK = !ATSRLATCH;
VD4.CK = !ATSRLATCH;
VD5.CK = !ATSRLATCH;
VD6.CK = !ATSRLATCH;
VD7.CK = !ATSRLATCH;

VD0.AR = CLR_SR;
VD1.AR = CLR_SR;
VD2.AR = CLR_SR;
VD3.AR = CLR_SR;
VD4.AR = CLR_SR;
VD5.AR = CLR_SR;
VD6.AR = CLR_SR;
VD7.AR = CLR_SR;


/*
 * Logic:  From CGA CLock V3 Note 640BW changed to BW640 due to Fitter errors
 * Logic symbols
 * ! NOT  # OR   & AND  $ XOR   .d D Flip-flop Input

 */

/* Always uses HIRES 25Mhz for Graphics or HIRES=1, 12.5Mhz when HIRES=0 for 40 column mode */
DOTCLK = (25MHZ & (GRPH # HIRES)) # (Qa & !HIRES & !GRPH); /* At Dot Clock top of CPLD */
DOTCLK2 = (25MHZ & (GRPH # HIRES)) # (Qa & !HIRES & !GRPH); /* At Dot Clock bottom of CPLD */

/* These conditions are required to cascade D Flip-Flops */
/* as a Counter !Q feeds back into D */
Qd.d = ((!Qd & Qc & Qb & Qa) # (Qd & !(Qc & Qb & Qa))) ;
Qc.d = ((!Qc & Qb & Qa) # (Qc & !(Qb & Qa))) ;
Qb.d = (!Qb & Qa # Qb & !Qa);
/* D Flip Flop Counter feeds !Q into D and uses 25MHZ to cycle */
Qa.d = !Qa;

Qa.AR = RESET;
Qa.CK = 25MHZ;
Qa.CE = 'b'1;
Qb.AR = RESET;
Qb.CK = 25MHZ;
Qb.CE = 'b'1;
Qc.AR = RESET;
Qc.CK = 25MHZ;
Qc.CE = 'b'1;
Qd.AR = RESET;
Qd.CK = 25MHZ;
Qd.CE = 'b'1;
CHCLK = Qc; /* Synonym of Character Clock used for delaying outputs, note 6845 uses INVCHCLK */


/* Using the condition (GRPH # HIRES) below means that even if the control register is set to GRPH=1 and HIRES=0 then will still use 80 column mode clock */
INVCHCLK=  (Qc & (GRPH & BW640 # !GRPH & HIRES)) # (GRPH & !BW640 & !Qc) # (Qd & !HIRES & !GRPH);

/* Character Latch now original SR Latch #3- 4 FF so don't need Colour pipeline */
CHLATCH=(!Qc & !Qa & !Qb & (HIRES # !HIRES & GRPH)) # (!Qd & !Qb & !Qc & !Qa & !HIRES & !GRPH); /* Pin Mapping now inverted including !Qa should make CHLatch a little earlier */

/* V3 Move back to clock cycle 4 from 3 in V2 - active low to high. For GRPH & 640BW=0 set ATSRLatch to CHLatch */
ATSRLATCH= ((Qc & !Qa & !Qb) & (GRPH & BW640 # !GRPH & HIRES)) # (Qd & !Qb & !Qc & !Qa & !HIRES & !GRPH) # (GRPH & !BW640 & Qc & Qa & Qb & !DOTCLK);

/* VA0 is based on inverted Qc delayed by 2 dot clocks */
CHATDLY.d = (!Qc & (GRPH # HIRES)) # ((Qb & !Qc & !Qd & !HIRES) # (!Qb & Qc & !Qd & !HIRES) # (Qb & Qc & !Qd & !HIRES) # (!Qb & !Qc & Qd & !HIRES));
CHATDLY.CK = 25MHZ;

CHAT.d = CHATDLY;
CHAT.CK = 25MHZ;

/*
 * Logic:  From CGA BlinkSync v2 Note
 * Logic symbols
 * ! NOT  # OR   & AND  $ XOR   .d D Flip-flop Input

 */

INVVS = 'b'0; /* Currently hard coded, could use top 2 bits of Control Register */
INVHS = 'b'1; /* Currently hard coded, could use top 2 bits of Control Register */

/*
 * Logic:  
 */
/* InvHS=0 then HSin output, InvHS=1 !HSin output, HS out will need to be delayed by 1 character clock */
HSYNC.d = (HS & !INVHS) # (!HS & INVHS); 
HSYNC.CK = CHCLK;
HSYNC.AR = RESET;

/* InvVS=0 then VSin output, InvVS=1 !VSin output */
VSYNC.d = (VS & !INVVS) # (!VS & INVVS);
VSYNC.CK = CHCLK;
VSYNC.AR = RESET;


VSQ0.CK=VS;
VSQ1.CK=VS;
VSQ2.CK=VS;
VSQ3.CK=VS;
VSQ4.CK=VS;
VSQ0.AR=RESET;
VSQ1.AR=RESET;
VSQ2.AR=RESET;
VSQ3.AR=RESET;
VSQ4.AR=RESET;

/* This counter divides VS 70Hz by 32 to give blink clock for cursor and blink */

VSQ0.d  =
   !VSQ0;

VSQ1.d  =
    !VSQ0 & VSQ1
  # VSQ0 & !VSQ1;

VSQ2.d  =
    !VSQ0 & VSQ1 & VSQ2
  # VSQ0 & VSQ1 & !VSQ2
  # !VSQ1 & VSQ2;

VSQ3.d  =
    !VSQ0 & VSQ1 & VSQ2 & VSQ3
  # VSQ0 & VSQ1 & VSQ2 & !VSQ3
  # !VSQ1 & VSQ2 & VSQ3
  # !VSQ2 & VSQ3;

VSQ4.d  =
    !VSQ0 & VSQ1 & VSQ2 & VSQ3 & VSQ4
  # VSQ0 & VSQ1 & VSQ2 & VSQ3 & !VSQ4
  # VSQ1 & VSQ2 & !VSQ3 & VSQ4
  # !VSQ2 & VSQ4
  # !VSQ1 & VSQ2 & VSQ4;

/* Inverted DE Display Enable internal currently */
BLANKDE  = !DE;
BLINKCHR = VSQ4; /* Character Blink is VS/32 */
BLINKCUR = VSQ3; /* Cursor Blink is VS/16 */

/*
 * Logic:  Control Register D5-D0, BLINKEN, BW640, VIDEOENABLE, BW, GRPH, HIRES
 * Logic symbols
 * ! NOT  # OR   & AND  $ XOR   .d D Flip-flop Input
 * Could remove external pins for ENBLINK, VIDEOENABLE & BW
 */
HIRES = 'b'1; /* Hard Code HIRES to 1 to do 80 Column Mode by default */
/* HIRES.d       = D0.io;   Control Port D0 MC39; */
GRPH.d        = D1.io;  /* Control Port D1 MC36; */
BW.d          = D2.io;  /* Control Port D2 MC35 */
/* VIDEOEN.d     = D3.io;   Control Port D3 MC34; */
VIDEOEN = 'b'1; /* Hard Code VIDEOEN to 1 to do enable CLR_SR */

BW640.d       = D4.io;  /* Control Port D4 MC40; */
BLINKEN.d     = D5.io;  /* Control Port D5 MC33; */

/* HIRES.CK       = CGACTL;  Control Port D0 MC39; */
GRPH.CK        = CGACTL;  /* Control Port D1 MC36; */
BW.CK          = CGACTL;  /* Control Port D2 MC35 */
/* VIDEOEN.CK     = CGACTL;  Control Port D3 MC34; */
BW640.CK       = CGACTL;  /* Control Port D4 MC40; */
BLINKEN.CK     = CGACTL;  /* Control Port D5 MC33; */

/*
 * Logic symbols
 * ! NOT  # OR   & AND  $ XOR   .d D Flip-flop Input
 * Logic symbols CGA Attribute Muliplexer V2
 */
C1 = (GRPH & !Qc & !Qb & VD7) # (GRPH & !Qc & Qb & VD5) # (GRPH & Qc & !Qb & VD3) # (GRPH & Qc & Qb & VD1);
C0 = (GRPH & !Qc & !Qb & VD6) # (GRPH & !Qc & Qb & VD4) # (GRPH & Qc & !Qb & VD2) # (GRPH & Qc & Qb & VD0);

I = !GRPH & ((ALPHADOTS & VD3) # (!ALPHADOTS & VD7 & !BLINKEN)); /* Video DAC P3, Alpha Mode = Fore Intensity, H - Back Intensity if BlinkEn=0 */
R = !GRPH & ((ALPHADOTS & VD2) # (!ALPHADOTS & VD6)); /* Video DAC P2, Alpha Mode Colour=Red VD2 or VD6 */
G = !GRPH & ((ALPHADOTS & VD1) # (!ALPHADOTS & VD5)); /* Video DAC P1, Alpha Mode Colour=Green VD1 or VD5 */
B = !GRPH & ((ALPHADOTS & VD0) # (!ALPHADOTS & VD4)); /* Video DAC P0, Alpha Mode Colour=Blue VD0 or VD4 */

AT7 = !GRPH & BLINKEN & VD7; /* Attribute 7 indicates blink when in alpha mode and control port blink enabled */ 


/*
 * Logic symbols from CGA Video Out V3
 * ! NOT  # OR   & AND  $ XOR   .d D Flip-flop Input
 */

/* This C Gate condition needed intermediate term as too complex for AlphaDots condition (in MDA), when this is true it turns off AlphaDots */
/* Syntax below evaluates to   !BlinkChr # !BlinkEn # !AT7 # Cursor */
/* Blink clock will provide alternating blink unless 2nd term returns 1, BlinkEn=0 returns 1, AT7=0 returns 1 and Cursor=1 returns 1*/

CGATE = !BLINKCHR # !(BLINKEN & AT7 & !CURSOR);

    /* Gates         A           B          C          */
    /* Note for A - !A NOR !B == A&B */
ALPHADOTS =  (CURSOR & BLINKCUR) #  (CGATE & SDOTS );

BLANKDE1.d = BLANKDE;  /* Delayed DE for pulsing CLR_SR at start of blanking period */
BLANKDE1.ar = RESET;
BLANKDE1.CK = CHCLK;

/* Note /CLR_SR active low but ! on pin mapping so +ve logic changed & to # */
CLR_SR = !VIDEOEN # (BLANKDE & !BLANKDE1) ; /* When video enable is low then output is supressed via CLR_SR, and 1st char clock of blank clears SR */
/*     1 during blank  1       1 corrected inverse on VS to HS */
OVERSCAN = BLANKDE & VSYNC & !HSYNC ; /* True when Display Enable=0 (Blank=1) and neither of HS or VS active (note HS inverted for 70Hz) */


/*
 * Logic symbols from Colour Multiplexer V3
 * ! NOT  # OR   & AND  $ XOR   .d D Flip-flop Input
 */

C1C0Z = !C1 & !C0;  /* True when C1=0 and C0=0 so need 4 Colour Backgroud, False when (C1=1 or C0=1) */


/* Since Video DAC P4=BWCol then no point in including in PLD B&W Palette values 0x10 - 0x1f, Colour Palette Values 0x00 - 0x0F */

/* Switches between colour and grey scale video palette */
P4 = BW;

/*   Text Mode=I,   4 Colour=Background_i,      640x200 and dot=CRInten,        640x200 background will be black */
P3 = (!GRPH & I) # (GRPH & !BW640 & CRBACKI) # (GRPH & BW640 & CRINTEN & ALPHADOTS); /* Video DAC P3 Intensity */

/*   Text Mode=R,   Overscan not 640=CRRed,        640x200 and dot=CRRed,         4 Colour foreground C1=1 or C0=1=C1,   4 Colour background C1=0 and C0=0 =CRRed */
P2 = (!GRPH & R) # (OVERSCAN & CRRED & !BW640) # (GRPH & BW640 & CRRED & ALPHADOTS) # (GRPH & !BW640 & !C1C0Z & C1) # (GRPH & !BW640 & C1C0Z & CRRED); /* Video DAC P2 Red */

/*   Text Mode=G,   Overscan not 640=CRGreen,        640x200 and dot=CRGreen,             4 Colour foreground C1=1 or C0=1=C0,   4 Colour background C1=0 and C0=0 =CRGreen */
P1 = (!GRPH & G) # (OVERSCAN & CRGREEN & !BW640) # (GRPH & BW640 & CRGREEN & ALPHADOTS) # (GRPH & !BW640 & !C1C0Z & C0)    # (GRPH & !BW640 & C1C0Z & CRGREEN); /* Video DAC P1 Green */

/*   Text Mode=B,   Overscan not 640=CRBlue,        640x200 and dot=CRBlue,             4 Colour foreground C1=1 or C0=CRColSl,   4 Colour background C1=0 and C0=0 =CRBlue */
P0 = (!GRPH & B) # (OVERSCAN & CRBLUE & !BW640) # (GRPH & BW640 & CRBLUE & ALPHADOTS) # (GRPH & !BW640 & !C1C0Z & CRCOLSL)    # (GRPH & !BW640 & C1C0Z & CRBLUE); /* Video DAC P0 Blue */

